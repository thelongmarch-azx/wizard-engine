// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def T = UnitTests.registerT("heap:", _, HeapTypeTester.new, _);
def X_ = void(
	T("cache", test_cache),
	T("canon_sig0", test_canon_sig0),
	T("canon_sig1", test_canon_sig1),
	T("canon_sig_i_i", test_canon_sig_i_i),
	T("canon_struct0", test_canon_struct0),
	T("canon_struct1", test_canon_struct1),
	T("canon_struct2", test_canon_struct2),
	T("canon_struct_super1", test_canon_struct_super1),
	T("canon_struct_super2", test_canon_struct_super2),
	T("canon_struct_rec1", test_canon_struct_rec1),
	T("canon_array0", test_canon_array0),
	T("canon_array1", test_canon_array1),
	T("canon_mixed0", test_canon_mixed0),
	T("canon_mixed1", test_canon_mixed1),
	T("valid_suptype0", test_valid_suptype0),
	T("valid_suptype0b", test_valid_suptype0b),
	T("valid_suptype1", test_valid_suptype1),
	T("valid_suptype1b", test_valid_suptype1b),
	T("invalid_suptype0", test_invalid_suptype0),
	T("invalid_suptype1", test_invalid_suptype1),
	T("invalid_suptype2", test_invalid_suptype2),
	T("invalid_suptype3", test_invalid_suptype3),
	()
);

def NO_SUPERS = ValueTypes.NO_HEAPTYPES;
def newSig = ValueTypes.newSig;
def newStruct = StructDecl.new(true, NO_SUPERS, _);
def newArray = ArrayDecl.new(true, NO_SUPERS, _);
def finalSig = SigDecl.new(true, _, _, _);
def finalStruct = StructDecl.new(true, _, _);
def finalArray = ArrayDecl.new(true, _, _);
def nonfinalSig = SigDecl.new(false, _, _, _);
def nonfinalStruct = StructDecl.new(false, _, _);
def nonfinalArray = ArrayDecl.new(false, _, _);

class HeapTypeTester(t: Tester) {
	def test_id_cached(a: Array<HeapTypeDecl>) {
		var cache = HeapTypeCache.new();
		var vec = Vector<HeapTypeDecl>.new();

		vec.puta(a);

		for (i < vec.length) {
			cache.doGroup(vec, i, 1);

			for (j < vec.length) {
				t.assert_eq(a[j], vec[j]);
			}
		}
	}
	def test_group_cached(first: Array<HeapTypeDecl>) {
		def N = 3;
		var cache = HeapTypeCache.new();
		var vec = Vector<HeapTypeDecl>.new();
		// init vector with {N} groups
		put_group(vec, first);
		for (i < (N - 1)) put_group(vec, Arrays.map(first, dupHeapTypeDecl));
		// run the comparator canonicalization
		for (i < N) {
			cache.doGroup(vec, i * first.length, first.length);
		}
		// check that all {N} groups are ref-equal to the first array
		for (i < N) {
			var j = i * first.length;
			for (k < first.length) {
				t.assert_eq(first[k], vec[j + k]);
			}
		}
	}
	def put_group(vec: Vector<HeapTypeDecl>, array: Array<HeapTypeDecl>) {
		for (i < array.length) {
			var d = array[i];
			d.recgrp_index = i;
			d.heaptype_index = vec.length;
			vec.put(d);
		}
	}
	def test_supertype_checking(pass: bool, d: HeapTypeDecl) {
		var error = ErrorGen.new("HeapTypeTest.v3");
		HeapTypeDecls.checkSupertypes(1, d, error);
		if (pass) {
			if (!error.ok()) t.fail1("expected pass, got %s", error.error_msg);
		} else {
			if (error.ok()) t.fail1("expected to fail supertype checking of %q", d.render);
		}
	}
}

def test_cache(t: HeapTypeTester) {
	var cache = HeapTypeCache.new();
	for (sig in [SigCache.i_i, SigCache.v_i]) {
		cache.insert(sig);
	}
	var s = newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]);
	cache.insert(s);
	var a = newArray([StorageType(ValueType.I32, Packedness.UNPACKED, true)]);
	cache.insert(a);
}

def test_canon_sig0(t: HeapTypeTester) {
	t.test_id_cached([
		SigCache.i_i,
		SigCache.v_i,
		SigCache.i_v,
		SigCache.ff_f,
		SigCache.dd_d
	]);
}

def test_canon_sig1(t: HeapTypeTester) {
	t.test_group_cached([
		newSig(SigCache.arr_i, SigCache.arr_i),
		newSig(SigCache.arr_v, SigCache.arr_i),
		newSig(SigCache.arr_ff, SigCache.arr_i)
	]);
}

def test_canon_sig_i_i(t: HeapTypeTester) {
	var cache = HeapTypeCache.new();
	var vec = Vector<HeapTypeDecl>.new();

	vec.put(SigCache.i_i);
	for (i < 4) {
		vec.put(newSig(SigCache.arr_i, SigCache.arr_i));
	}

	for (i < vec.length) {
		cache.doGroup(vec, i, 1);
	}

	for (j < vec.length) {
		t.t.assert_eq(SigCache.i_i, vec[j]);
	}
}

def test_canon_struct0(t: HeapTypeTester) {
	t.test_id_cached([
		newStruct([]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, false), StorageType(ValueType.V128, Packedness.UNPACKED, true)])
	]);
}

def test_canon_struct1(t: HeapTypeTester) {
	t.test_group_cached([
		newStruct([]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, false), StorageType(ValueType.V128, Packedness.UNPACKED, true)])
	]);
}

def test_canon_struct2(t: HeapTypeTester) {
	var cache = HeapTypeCache.new();
	var heaptypes = Vector<HeapTypeDecl>.new();

	var s0 = newStruct([]);
	var s1 = newStruct([]);
	var s2 = newStruct([StorageType(ValueType.Ref(true, HeapType.Struct(s1)), Packedness.UNPACKED, true)]);

	t.put_group(heaptypes, [s0]);
	cache.doGroup(heaptypes, 0, 1);

	t.put_group(heaptypes, [s1, s2]);
	cache.doGroup(heaptypes, 1, 2);
	t.t.assert_eq(s1, heaptypes[1]);
	t.t.assert_eq(s2, heaptypes[2]);

	var ft = s2.field_types[0];
	t.t.assert_eq(Packedness.UNPACKED, ft.pack);
	t.t.assert_eq(true, ft.mutable);
	t.t.assert_eq(ValueType.Ref(true, HeapType.Struct(s1)), ft.valtype);
}

def test_canon_struct_super1(t: HeapTypeTester) {
	var cache = HeapTypeCache.new();
	var heaptypes = Vector<HeapTypeDecl>.new();

	var s0 = newStruct([]);
	var s1 = newStruct([]);
	var s2 = finalStruct([HeapType.Struct(s1)], [StorageType(ValueType.Ref(true, HeapType.Struct(s1)), Packedness.UNPACKED, true)]);
	t.t.assert_eq(HeapType.Struct(s1), s2.supertypes[0]);

	t.put_group(heaptypes, [s0]);
	cache.doGroup(heaptypes, 0, 1);

	t.put_group(heaptypes, [s1, s2]);
	cache.doGroup(heaptypes, 1, 2);
	t.t.assert_eq(s1, heaptypes[1]);
	t.t.assert_eq(s2, heaptypes[2]);

	var ft = s2.field_types[0];
	t.t.assert_eq(Packedness.UNPACKED, ft.pack);
	t.t.assert_eq(true, ft.mutable);
	t.t.assert_eq(ValueType.Ref(true, HeapType.Struct(s1)), ft.valtype);
	t.t.assert_eq(HeapType.Struct(s1), s2.supertypes[0]);
}

def test_canon_struct_super2(t: HeapTypeTester) {
	var cache = HeapTypeCache.new();
	var heaptypes = Vector<HeapTypeDecl>.new();

	var s0 = newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]);
	var st = StorageType(ValueType.Ref(true, HeapType.Struct(s0)), Packedness.UNPACKED, true);
	var s1 = finalStruct([HeapType.Struct(s0)], [st]);
	var s2 = newStruct([StorageType(ValueType.F32, Packedness.UNPACKED, true)]);
	var s3 = finalStruct([HeapType.Struct(s2)], [st]);

	t.put_group(heaptypes, [s0, s1]);
	cache.doGroup(heaptypes, 0, 2);

	t.put_group(heaptypes, [s2, s3]);
	cache.doGroup(heaptypes, 2, 2);

	t.t.assert_eq(s0, heaptypes[0]);
	t.t.assert_eq(s1, heaptypes[1]);
	t.t.assert_eq(s2, heaptypes[2]);
	t.t.assert_eq(s3, heaptypes[3]);
}

def test_canon_struct_rec1(t: HeapTypeTester) {
	var pre = StorageType(ValueType.I32, Packedness.UNPACKED, true);
	var s0 = newStruct([pre]);
	var s1 = newStruct([pre]);
	var s2 = newStruct([pre]);

	s0.field_types[0] = StorageType(ValueType.Ref(false, HeapType.Struct(s0)), pre.pack, pre.mutable); // self-recursive
	s1.field_types[0] = StorageType(ValueType.Ref(false, HeapType.Struct(s2)), pre.pack, pre.mutable); // mutually-recursive
	s2.field_types[0] = StorageType(ValueType.Ref(false, HeapType.Struct(s1)), pre.pack, pre.mutable); // mutually-recursive

	t.test_group_cached([s0, s1, s2]);
}

def test_canon_array0(t: HeapTypeTester) {
	t.test_id_cached([
		newArray([]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, false)])
	]);
}

def test_canon_array1(t: HeapTypeTester) {
	t.test_group_cached([
		newArray([]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, false)])
	]);
}

def test_canon_mixed0(t: HeapTypeTester) {
	t.test_id_cached([
		SigCache.i_i,
		SigCache.ff_f,
		newStruct([]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, false)])
	]);
}

def test_canon_mixed1(t: HeapTypeTester) {
	t.test_group_cached([
		SigCache.i_i,
		SigCache.ff_f,
		newStruct([]),
		newStruct([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, true)]),
		newArray([StorageType(ValueType.I32, Packedness.UNPACKED, false)])
	]);
}

def test_valid_suptype0(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var mut_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, true);

	var s0 = newStruct(no_fields);
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], no_fields));
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [mut_int_field]));

	var a0 = finalArray(NO_SUPERS, [mut_int_field]);
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [mut_int_field]));

	var f0 = SigCache.v_v;
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, SigCache.arr_v));
}

def test_valid_suptype0b(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var mut_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, true);

	var s0 = nonfinalStruct(NO_SUPERS, no_fields);
	t.test_supertype_checking(true, finalStruct([HeapType.Struct(s0)], no_fields));
	t.test_supertype_checking(true, finalStruct([HeapType.Struct(s0)], [mut_int_field]));

	var a0 = nonfinalArray(NO_SUPERS, [mut_int_field]);
	t.test_supertype_checking(true, finalArray([HeapType.Array(a0)], [mut_int_field]));

	var f0 = nonfinalSig(NO_SUPERS, [], []);
	t.test_supertype_checking(true, finalSig([HeapType.Func(f0)], SigCache.arr_v, SigCache.arr_v));
}

def test_valid_suptype1(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var imm_anyref_field = StorageType(ValueTypes.ANYREF, Packedness.UNPACKED, false);
	var imm_funcref_field = StorageType(ValueTypes.FUNCREF, Packedness.UNPACKED, false);
	var imm_nullref_field = StorageType(ValueTypes.NULLREF, Packedness.UNPACKED, false);

	var s0 = newStruct([imm_anyref_field]);
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_anyref_field]));
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_nullref_field]));
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_funcref_field]));

	var a0 = finalArray(NO_SUPERS, [imm_anyref_field]);
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [imm_anyref_field]));

	var f0 = finalSig(NO_SUPERS, SigCache.arr_v, [ValueTypes.ANYREF]);
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.ANYREF])); // covariant ret
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.NULLREF])); // covariant ret
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.FUNCREF])); // covariant ret

	var f1 = finalSig(NO_SUPERS, [ValueTypes.NULLREF], SigCache.arr_v);
	t.test_supertype_checking(false, finalSig([HeapType.Func(f1)], [ValueTypes.NULLREF], SigCache.arr_v)); // contra-variant param
	t.test_supertype_checking(false, finalSig([HeapType.Func(f1)], [ValueTypes.ANYREF], SigCache.arr_v)); // contra-variant param
	t.test_supertype_checking(false, finalSig([HeapType.Func(f1)], [ValueTypes.FUNCREF], SigCache.arr_v)); // contra-variant param
}

def test_valid_suptype1b(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var imm_anyref_field = StorageType(ValueTypes.ANYREF, Packedness.UNPACKED, false);
	var imm_funcref_field = StorageType(ValueTypes.FUNCREF, Packedness.UNPACKED, false);
	var imm_nullref_field = StorageType(ValueTypes.NULLREF, Packedness.UNPACKED, false);

	var s0 = nonfinalStruct(NO_SUPERS, [imm_anyref_field]);
	t.test_supertype_checking(true, finalStruct([HeapType.Struct(s0)], [imm_anyref_field]));
	t.test_supertype_checking(true, finalStruct([HeapType.Struct(s0)], [imm_nullref_field]));
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_funcref_field]));

	var a0 = nonfinalArray(NO_SUPERS, [imm_anyref_field]);
	t.test_supertype_checking(true, finalArray([HeapType.Array(a0)], [imm_anyref_field]));

	var f0 = nonfinalSig(NO_SUPERS, SigCache.arr_v, [ValueTypes.ANYREF]);
	t.test_supertype_checking(true, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.ANYREF])); // covariant ret
	t.test_supertype_checking(true, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.NULLREF])); // covariant ret
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, [ValueTypes.FUNCREF])); // covariant ret

	var f1 = nonfinalSig(NO_SUPERS, [ValueTypes.NULLREF], SigCache.arr_v);
	t.test_supertype_checking(true, finalSig([HeapType.Func(f1)], [ValueTypes.NULLREF], SigCache.arr_v)); // contra-variant param
	t.test_supertype_checking(true, finalSig([HeapType.Func(f1)], [ValueTypes.ANYREF], SigCache.arr_v)); // contra-variant param
	t.test_supertype_checking(false, finalSig([HeapType.Func(f1)], [ValueTypes.FUNCREF], SigCache.arr_v)); // contra-variant param
}

def test_invalid_suptype0(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var mut_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, true);

	var s0 = newStruct(no_fields);
	var a0 = finalArray(NO_SUPERS, [mut_int_field]);
	var f0 = SigCache.v_v;

	t.test_supertype_checking(false, finalStruct([HeapType.Array(a0)], no_fields)); // kind mismatch
	t.test_supertype_checking(false, finalStruct([HeapType.Func(f0)], no_fields)); // kind mismatch

	t.test_supertype_checking(false, finalArray([HeapType.Struct(s0)], [mut_int_field])); // kind mismatch
	t.test_supertype_checking(false, finalArray([HeapType.Func(f0)], [mut_int_field])); // kind mismatch

	t.test_supertype_checking(false, finalSig([HeapType.Struct(s0)], SigCache.arr_v, SigCache.arr_i)); // kind mismatch
	t.test_supertype_checking(false, finalSig([HeapType.Array(a0)], SigCache.arr_v, SigCache.arr_i)); // kind mismatch
}

def test_invalid_suptype1(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var mut_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, true);
	var imm_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, false);

	var s0 = newStruct([mut_int_field]);
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], no_fields)); // no fields
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_int_field])); // make mut field imm

	var a0 = finalArray(NO_SUPERS, [mut_int_field]);
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [imm_int_field])); // make mut field imm

	var f0 = SigCache.v_v;
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_v, SigCache.arr_i)); // arity mismatch
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], SigCache.arr_i, SigCache.arr_v)); // arity mismatch
}

def test_invalid_suptype2(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var mut_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, true);
	var mut_anyref_field = StorageType(ValueTypes.ANYREF, Packedness.UNPACKED, true);
	var mut_funcref_field = StorageType(ValueTypes.FUNCREF, Packedness.UNPACKED, true);

	var s0 = nonfinalStruct(NO_SUPERS, [mut_anyref_field]);
	t.test_supertype_checking(true, finalStruct([HeapType.Struct(s0)], [mut_anyref_field])); // ok
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [mut_int_field])); // field type mismatch
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [mut_funcref_field])); // field type mismatch

	var a0 = nonfinalArray(NO_SUPERS, [mut_anyref_field]);
	t.test_supertype_checking(true, finalArray([HeapType.Array(a0)], [mut_anyref_field])); // ok
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [mut_int_field])); // elem type mismatch
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [mut_funcref_field])); // elem type mismatch

	var f0 = nonfinalSig(NO_SUPERS, [ValueTypes.ANYREF], [ValueTypes.FUNCREF]);
	t.test_supertype_checking(true, finalSig([HeapType.Func(f0)], [ValueTypes.ANYREF], [ValueTypes.FUNCREF])); // ok
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], [ValueType.I32], [ValueTypes.FUNCREF])); // param type
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], [ValueTypes.FUNCREF], [ValueTypes.FUNCREF])); // param type
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], [ValueTypes.ANYREF], [ValueType.I32])); // result type
	t.test_supertype_checking(false, finalSig([HeapType.Func(f0)], [ValueTypes.ANYREF], [ValueTypes.ANYREF])); // result type
}

def test_invalid_suptype3(t: HeapTypeTester) {
	var no_fields = Array<StorageType>.new(0);
	var imm_int_field = StorageType(ValueType.I32, Packedness.UNPACKED, false);
	var imm_anyref_field = StorageType(ValueTypes.ANYREF, Packedness.UNPACKED, false);
	var imm_funcref_field = StorageType(ValueTypes.FUNCREF, Packedness.UNPACKED, false);

	var s0 = newStruct([imm_funcref_field]);
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_int_field])); // field type mismatch
	t.test_supertype_checking(false, finalStruct([HeapType.Struct(s0)], [imm_anyref_field])); // field type mismatch

	var a0 = finalArray(NO_SUPERS, [imm_funcref_field]);
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [imm_int_field])); // elem type mismatch
	t.test_supertype_checking(false, finalArray([HeapType.Array(a0)], [imm_anyref_field])); // elem type mismatch
}

def dupHeapTypeDecl(decl: HeapTypeDecl) -> HeapTypeDecl {
	match (decl) {
		x: SigDecl => return newSig(x.params, x.results);
		x: StructDecl => return newStruct(x.field_types);
		x: ArrayDecl => return newArray(x.elem_types);
	}
	return null;
}
// TODO: complex sigs, structs, arrays
// TODO: recursive sigs, structs, arrays
